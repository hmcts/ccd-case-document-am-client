plugins {
  id 'java-library'
  id 'maven-publish'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'org.springframework.boot' version '3.5.4'
  id 'uk.gov.hmcts.java' version '0.12.67'
  id 'com.github.ben-manes.versions' version '0.52.0'
}

def versions = [
    lombok      : '1.18.38',
    springCloud : '2025.0.0',
    openfeign   : '13.6'
]

def buildNumber = System.getenv("RELEASE_VERSION")?: "DEV-SNAPSHOT"

group = 'com.github.hmcts'
version = buildNumber

dependencyUpdates.resolutionStrategy = {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

compileJava {
  options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
  options.compilerArgs << '-Xlint:deprecation'
}

repositories {
  mavenCentral()
}

jar {
  manifest {
    attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
  }
  enabled = true
  archiveClassifier.set("")
}

bootJar {
  enabled = false
}

tasks.register('printVersion') {
  doLast {
    print project.version
  }
}

def pomConfig = {
  licenses {
    license {
      name "MIT License"
      url "http://www.opensource.org/licenses/mit-license.php"
      distribution "repo"
    }
  }

  scm {
    url "https://github.com/hmcts/ccd-case-document-am-client"
  }
}

tasks.register('sourcesJar', Jar) {
  dependsOn classes
  archiveClassifier.set('sources')
  from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar) {
  dependsOn javadoc
  archiveClassifier.set('javadoc')
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

publishing {
  publications {
    Main(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar
      groupId = project.group
      artifactId = 'ccd-case-document-am-client'
      version = project.version

      pom.withXml {
        def root = asNode()
        root.appendNode('description', 'Client library for communicating with CCD case document am client api')
        root.appendNode('name', 'CCD case document am client')
        root.appendNode('url', 'https://github.com/hmcts/ccd-case-document-am-client')

        root.children().last() + pomConfig
      }
    }
  }
  repositories {
    maven {
      name = "AzureArtifacts"
      url = uri("https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1")
      credentials {
        username = System.getenv("AZURE_DEVOPS_ARTIFACT_USERNAME")
        password = System.getenv("AZURE_DEVOPS_ARTIFACT_TOKEN")
      }
    }
  }
}

dependencyManagement {
  applyMavenExclusions = false
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${versions.springCloud}"
  }
}

dependencies {
  implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-parameter-names'

  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign'
  implementation group: 'io.github.openfeign', name: 'feign-httpclient', version: versions.openfeign
  implementation group: 'io.github.openfeign', name: 'feign-jackson', version: versions.openfeign
  implementation group: 'io.github.openfeign', name: 'feign-okhttp', version: versions.openfeign
  implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.19.2'

  compileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok

  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
  testImplementation group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock', {
    exclude group: 'com.github.tomakehurst', module: 'wiremock-standalone'
  }
  testImplementation 'commons-io:commons-io:2.20.0'
  testImplementation group: 'com.github.tomakehurst', name: 'wiremock-standalone', version: '3.0.1'
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}

tasks.withType(Test).configureEach {
  useJUnitPlatform()
}
